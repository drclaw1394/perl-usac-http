#!/usr/bin/env perl
#
use v5.36;
our $VERSION="v0.1.0";

#no warnings "experimental";

BEGIN{
  # Before we even attempt setup, check if help is needed
  if(grep /-{1,2}h/, @ARGV){
    require Pod::Usage;                    
    Pod::Usage::pod2usage(verbose=>1, );
  }
}

use Log::ger;
use Log::ger::Output "Screen";
use Log::OK {
	lvl=>"info",
	opt=>"verbose",
};


#use EV;
use AnyEvent;
use Import::These qw<uSAC:: Util ::HTTP:: Server ::Middleware:: Static Log>;

my %options;

# Parses a string into a site specification hash
#
sub parse_site_spec{
  no warnings "experimental";
  use builtin <trim >;
  my @full=qw<id prefix>;
  my %spec;

  $spec{id}=undef;        # Default id is  a new site
  $spec{prefix}="";     # Default to no prefix

  my $input=trim $_[0];
  
  for my $field(split ",", $input){

    unless($field=~/=/){
      # Not a key value pair
      if($field=~ s/^#//){
        say "ID";
        #ID specificaiton
        $spec{id}=$field;
        #$spec{prefix}="";
      }
      else {
        say "PREFIX";
        #it looks like a normal prefix, so treat as such
        #$spec{id}=undef;
        $spec{prefix}=$field;
      }
      #Assume only a site PREFIX, with random id

    }
    else {
      # Key value pairs
      for my ($key, $value)(split "=", $field, 2){

        $key=trim $key;
        $value=trim $value;

        my @val;

        #Ensure only 0 or 1 keys match
        die "Ambiguous field name: $key" if 2<=grep /^$key/i, @full;

        # Find the first item that starts with the shorthand
        ($key)=grep /^$key/i, @full;
        $spec{$key}=$value;
      }
    }
  }

  \%spec;
}



# Welcome
my $welcome= "uSAC HTTP Server $VERSION - Copyright 2023 Ruben Westerberg
";
Log::OK::INFO and log_info $welcome;


# Map of ID to sites
my %sites;

# Simple wrapper containing all sub sites. This allows the server to be created
# at the end and simply add this group
#
my $server_proxy=uSAC::HTTP::Site->new(id=>"/");

# The top level server always has a prefix of "/". Add the  proxy so we can
# find it again
$sites{"/"}=$server_proxy;




my $current=$server_proxy;
my %cur_opts=(list_dir=>1);


my $counter=0;
my @unkown;
while(@ARGV){
  $_=shift; # from @ARGV
  if(/-{1,2}site/){
    $_=shift;
    my $spec= parse_site_spec($_);

    # Create a new id if none specified
    $spec->{id}//=$counter++;

    if(exists($sites{$spec->{id}})){
      my $found=$sites{$spec->{id}};

      # an existing site
      #warn "Site prefix changed from ".$found->id. "to $spec->{id}" if $found->id ne $spec->{id};
      $current=$found; 
    }
    else {
      # a new site

      my $new=uSAC::HTTP::Site->new($spec->%*);
      $sites{$spec->{id}}=$new;
      $current->add_site($new);
      $current=$new;
    }

    next;
  }

  elsif(/^-{1,2}listen/){

    $options{listen}=shift;
    die "Listen argument required" unless $options{listen};
    #$options{"listen"}||=["[::]:8000"];                                                                                 #
  }

  elsif(/^-{1,2}(no-)?list-dir/){
    $cur_opts{list_dir}=!$1;
  }

  elsif(/^-{1,2}(no-)?log/){
    $cur_opts{log}=!$1;
  }
  elsif(/^--/){
    # End processing of options  
    last;
  }
  elsif(/^[^-]/){
    # Treat as path to file or dir
    #convert to abs for intended behaviour	
    my $target=uSAC::Util::rel2abs($_, uSAC::Util::cwd);

    if(/\.psgi$/i){
      # Add explicit PSGI middleware with path to application
      # uses site prefix  for mounting
      #
      Log::OK::INFO and log_info "Loading PSGI from source:\n $target";
      require uSAC::HTTP::Middleware::PSGI;
      $current->add_route("", uSAC::HTTP::Middleware::PSGI::uhm_psgi($_));
    }
    elsif(/\.route$|\.pl$/){
      Log::OK::INFO and log_info "Loading Routes from source:\n $target";
      # perl script to run as  
      $current->load($target);
    }
    else {
      # Assume a dir for serving static files from
      Log::OK::INFO and log_info "Serving static files from source:\n $target";
      $current->add_route("", uhm_static_root roots=>[$_], %cur_opts);
    }
  }
  else {
    # Known options
  }

}


$options{"listen"}//=["[::]:8000"];
# Create server
#
my $server=uSAC::HTTP::Server->new(listen=>$options{listen});


# Inject logging if logging enabled
#
$server->add_middleware( uhm_log dump_headers=>1, color=>1) if $options{log};
$server->add_site($server_proxy);

# Configure
my @options=@unkown;
$server->parse_cli_options(@options);

# Run!
$server->run();


__END__

=head1 TITLE

usac -  Utility HTTP Server

=head1 SYNOPSIS

  #mount psgi applications and serve static files
	usac --site id=Admin,prefix=admin/ path/to/admin.psgi --site #User,user/ path/to/user.psgi  --site #/ path/to/static/dir

  # /               
  #   admin/
  #      routes...
  #
  #   user/
  #     routes....
  #
  # static files

=head1 DESCRIPTION

usac is a HTTP for serving static files preconfigured routes and piping
websockets from the command line. It is a application wrapper around
uSAC::HTTP.

It is flexible to allow constructing prefixed grouping of routes, mounting
'applications' into different names spaces.


Options and input files are interleved, and interact in a stateful manner.


=head1 OPTIONS



=over

=item help

Show detailed usage information

=item listen

Network interfaces to listen on. Multiple accepted.

=item site

    --site id=ID
    --site ID

Selects a existing site with id of ID.

  --site id=id,prefix=>prefix

Creates a new site with

Add a group ( a "site") for the following routes/static files to be placed
under. Useful  for defining a prefix and/or host specific routes.

If the site id exists, the existing site is choosen and the newly specified
prefix is ignored. Otherwise a new site is created and added as a sub site to
the current site, with the prefix provided (if any)

The top level site is "/" which is automatically selected.

=item list-dir no-list-dir

Enable/disable listing of directories for static file serving

=item log

Enable logging of requests


=back

# TODO:
#	--site prefix=asdf --ws route_name --in file:// --out 
# --site prefix=asdf --r route_name  --in file:// fd:// http://proxy

#
#	--mqtt my/url=>mqtt://host:port/topic		#subscribes to a topic and pushes the newest
#							#message out to incomming connections.
#							#if accept is multipart then mutiple messages	

# Currently Working on:
#	--static-file /prefix=path_to_dir_with_static_files
#	--static-dir /prefix=path_to_dir_list		#uses accepts to determing format
#		
#
#create server instance;
#load any config file specified

#set default listener if non provided


